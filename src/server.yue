import "yue"
const json = do
	local success, result = nil, nil

	if success, result := pcall(require, "cojson")
		result
	elseif success, result := pcall(require, "cjson")
		result
	elseif success, result := pcall(require, "json")
		result
	else
		error("Could not find a JSON-module for YueScript! Please install one.")


const send = (data, raw = false) ->
	if not raw
		data = string.gsub((json.dump or json.encode)(data), "[\r\n]", "")

	assert(type(data) == "string")

	io.stdout::write(data, "\n")
	io.stdout::flush()
	return


const recieve = () ->
	const rawData = io.stdin::read("*l")

	const data = if type(rawData) == "string"
		(json.load or json.decode)(rawData)
	else
		nil

	data, rawData


const eventHandlers = {
	textChange: {
		(input) ->
			const sourceCode = string.gsub(input.sourceCode, "%s*$", "")
			const success, astOrError, transpiledLuaCode = yue.check(sourceCode)

			send({
				:success
				-- :transpiledLuaCode
				-- :rawInput
				[success and "ast" or "error"]: astOrError
			})
	},
}


while true
	const input, rawInput = recieve()

	if input == nil
		send("{}", true)
		return

	assert(type(input.event) == "string")

	for fn in *(eventHandlers[input.event] ?? {})
		fn(input.payload)
